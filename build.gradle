buildscript {
	repositories {
		mavenLocal()
		maven {
			credentials {
				username nexusUser
				password nexusPassword
			}
			url esdkSnapshotURL
		}
		maven {
			credentials {
				username nexusUser
				password nexusPassword
			}
			url esdkReleaseURL
		}
		jcenter()
	}
	dependencies {
		classpath "de.abas.esdk:gradlePlugin:$version"
	}
}

plugins {
	id "java"
	id "maven-publish"
}

apply plugin: "de.abas.esdk"

repositories {
	mavenLocal()
	jcenter()
	maven {
		url esdkSnapshotURL
		credentials {
			username nexusUser
			password nexusPassword
		}
	}
	maven {
		credentials {
			username nexusUser
			password nexusPassword
		}
		url esdkReleaseURL
	}
	maven { url "http://$NEXUS_HOST:$NEXUS_PORT/nexus/content/repositories/$NEXUS_NAME" }
	maven { url "http://$NEXUS_HOST:$NEXUS_PORT/nexus/content/repositories/$NEXUS_NAME-SNAPSHOT" }
}

esdk {
	app {
		name = "trainingApp"
		vendorId = "ag"
		appId = "train"
		shared = true
		infosystems = ["IS.OW1.TESTINFO"]
		tables = ["TestDb"]
		data = ["data.json"]
		keys = ["2738"]
		enums = ["FILE_FORMAT"]
		screens = ["Customer:Customer": ["A"], "Sales:BlanketOrder": ["A"], "Operation:Operation": ["A"], "Pricing:Pricing": ["A"], "TestDb:TestStructure": ["A"]]
		essentialsVersions = ["2016r1n00-2016r4n16", "2017r1n00-2017r4n16", "2018r1n00-2018r4n16"]
		preconditions = ["workDirs=ow1"]
		languages = "DEA"
		workdirs = ["ow1", "owbi"]
	}

	abas {
		homeDir = ABAS_HOMEDIR
		clientDir = ABAS_CLIENTDIR
		clientId = ABAS_CLIENTID
		edpHost = EDP_HOST
		edpPort = EDP_PORT.toInteger()
		edpUser = EDP_USER
		edpPassword = EDP_PASSWORD
	}

	nexus {
		nexusHost = NEXUS_HOST
		nexusPort = NEXUS_PORT.toInteger()
		nexusRepoName = NEXUS_NAME
		nexusPassword = NEXUS_PASSWORD
	}

	ssh {
		host = SSH_HOST
		port = SSH_PORT.toInteger()
		user = SSH_USER
		password = SSH_PASSWORD
		key = SSH_KEY
	}
	installType = "SSH"
}

task printVersion() {
	doLast {
		println(version)
	}
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

compileJava {
	options.encoding = "UTF-8"
}

configurations {
	installer
}

configurations.installer {
	resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
}

group = 'de.abas.esdk.app'

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId 'de.abas.esdk.app'
			artifactId 'trainingApp'
			artifact createAppJar
		}
	}
	repositories {
		if (project.hasProperty("esdkSnapshotURL") && project.hasProperty("esdkReleaseURL")
				&& project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")) {
			if (version.endsWith('-SNAPSHOT')) {
				maven {
					url esdkSnapshotURL
					credentials {
						username nexusUser
						password nexusPassword
					}
				}
			} else {
				maven {
					url esdkReleaseURL
					credentials {
						username nexusUser
						password nexusPassword
					}
				}
			}
		}
	}
}

task downloadInstaller(type: Copy) {
	dependsOn configurations.installer
	from {
		configurations.installer.singleFile
	}
	into file("${project.buildDir}/installer/")
}

dependencies {
	installer group: "de.abas.esdk", name: "installer", version: version, classifier: "", ext: 'zip',
			changing: true
	testCompile "junit:junit:4.12"
	testCompile 'org.hamcrest:hamcrest-all:1.3'
}
